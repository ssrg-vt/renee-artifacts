ConditionHolds [ (IMPORTING arm_state) p : s ] : THEORY

  BEGIN

  %// ConditionHolds()
  %// ================
  %// Return TRUE iff COND currently holds
IMPORTING field

 ConditionHolds_1( cond_ : bvec[4] ) : bool =  %// Evaluate base condition.
   let cond_1_3 = field(4, cond_ , 1, 3) in 
    cond cond_1_3 = bv[3](0b000) ->  ( p`PSTATE`NZCV(1) = true ) ,%// EQ or NE
         cond_1_3 = bv[3](0b100) ->  ( p`PSTATE`NZCV(2) = true ) ,% // CS or CC
         cond_1_3 = bv[3](0b010) ->  ( p`PSTATE`NZCV(0) = true ) ,% // MI or PL
         cond_1_3 = bv[3](0b110) ->  ( p`PSTATE`NZCV(3) = true ) ,% // VS or VC
         cond_1_3 = bv[3](0b001) ->  ( p`PSTATE`NZCV(2) = true & p`PSTATE`NZCV(1) = false), %// HI or LS
         cond_1_3 = bv[3](0b101) ->  ( p`PSTATE`NZCV(0) = p`PSTATE`NZCV(3)) ,% // GE or LT
         cond_1_3 = bv[3](0b011) ->  ( p`PSTATE`NZCV(0) = p`PSTATE`NZCV(3) & p`PSTATE`NZCV(1) = false) ,% // GT or LE
         else                    ->  TRUE %// AL
     endcond

         %// Condition flag values in the set '111x' indicate always true
	 %// Otherwise, invert condition if necessary. 

ConditionHolds( cond_ : bvec[4] ) : bool =
            if cond_(0) = true & cond_ /= bv[4](0b1111)
                 then NOT ( ConditionHolds_1(cond_))
            else ConditionHolds_1(cond_ )
	    endif

  END ConditionHolds
